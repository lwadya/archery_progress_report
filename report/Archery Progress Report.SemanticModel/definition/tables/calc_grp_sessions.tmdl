/// Calculation group for groupings of sessions
table calc_grp_sessions
	lineageTag: e0fef639-72d4-427c-a750-e8f3c91647dd

	calculationGroup
		precedence: 2

		calculationItem 'Default Selection' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2024-09-05
				// Description:     Selected measure with no modifications
				
				SELECTEDMEASURE()

		calculationItem 'Selected Sessions' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2024-12-30
				// Description:     Selected session, or last session if none are selected
				
				IF(
				    ISFILTERED( dim_calendar ),
				    SELECTEDMEASURE(),
				    CALCULATE(
				        SELECTEDMEASURE(),
				        calc_grp_sessions[sessions] = "Last Session"
				    )
				)

		calculationItem 'Best Session' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2024-12-31
				// Description:     Best session ever, by top round score average
				
				VAR _round_scores_table =
				SUMMARIZE(
				    ALL( fact_shot_score ),
				    fact_shot_score[session_id],
				    "round_score_average", [Round Score Average]
				)
				
				VAR _best_session_id =
				MAXX(
				    TOPN(
				        1,
				        _round_scores_table,
				        [round_score_average], DESC
				    ),
				    [session_id]
				)
				
				VAR _result =
				CALCULATE(
				    SELECTEDMEASURE(),
				    FILTER(
				        ALL( dim_session ),
				        dim_session[session_id] = _best_session_id
				    )
				)
				
				RETURN _result

		calculationItem 'Last Session' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2024-12-31
				// Description:     Last session
				
				CALCULATE(
				    SELECTEDMEASURE(),
				    TOPN(
				        1,
				        ALL( dim_session ),
				        dim_session[session_id], DESC
				    )
				)

		calculationItem 'Last 5 Sessions' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2024-12-31
				// Description:     Last 5 sessions
				
				CALCULATE(
				    SELECTEDMEASURE(),
				    TOPN(
				        5,
				        ALL( dim_session ),
				        dim_session[session_id], DESC
				    )
				)

		calculationItem 'Last 10 Sessions' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2024-12-31
				// Description:     Last 10 sessions
				
				CALCULATE(
				    SELECTEDMEASURE(),
				    TOPN(
				        10,
				        ALL( dim_session ),
				        dim_session[session_id], DESC
				    )
				)

		calculationItem 'All Sessions' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2024-09-05
				// Description:     All sessions
				
				CALCULATE(
				    SELECTEDMEASURE(),
				    ALL( dim_session )
				)

		calculationItem 'Selected Range' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2025-02-14
				// Description:     Sessions selected using the range selection tables
				
				VAR _default_session_range = 30
				
				VAR _range_end_session_id =
				SELECTEDVALUE(
				    select_session_end_date_time_series[session_id],
				    MAX( select_session_end_date[session_id] )
				)
				
				VAR _range_length = SELECTEDVALUE( select_session_range[sessions], _default_session_range )
				
				VAR _result =
				CALCULATE(
				    SELECTEDMEASURE(),
				    dim_session[session_id] > _range_end_session_id - _range_length && dim_session[session_id] <= _range_end_session_id
				)
				
				RETURN _result

		calculationItem 'Selected Adjustments' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2025-02-18
				// Description:     All sessions between (inclusive) the selected major adjustments
				//                  If no major adjustments are selected, use the latest one as the selection
				
				VAR _selection_latest_adjustment_id = MAX( select_major_adjustment[session_id] )
				
				VAR _selection_earliest_adjustment_id =
				IF(
				    ISFILTERED( select_major_adjustment ),
				    MIN( select_major_adjustment[session_id] ),
				    _selection_latest_adjustment_id
				)
				
				VAR _result =
				CALCULATE(
				    SELECTEDMEASURE(),
				    FILTER(
				        ALL( dim_session ),
				        AND( dim_session[latest_adjustment_session_id] >= _selection_earliest_adjustment_id, dim_session[latest_adjustment_session_id] <= _selection_latest_adjustment_id )
				    )
				)
				
				RETURN _result

		calculationItem 'Selected Adjustments Time Series' =
				
				// Last Developer:  Lukas Wadya
				// Last Updated:    2025-02-18
				// Description:     Only display values for all sessions between (inclusive) the selected major adjustments
				//                  If no major adjustments are selected, use the latest one as the selection
				
				VAR _selection_earliest_adjustment_id =
				CALCULATE(
				    [Session ID Min],
				    calc_grp_sessions[sessions] = "Selected Adjustments"
				)
				
				VAR _selection_latest_adjustment_id =
				CALCULATE(
				    [Session ID Max],
				    calc_grp_sessions[sessions] = "Selected Adjustments"
				)
				
				VAR _dim_session_selected_adjustment_id = SELECTEDVALUE( dim_session[latest_adjustment_session_id] )
				
				VAR _result =
				IF(
				    AND( _dim_session_selected_adjustment_id >= _selection_earliest_adjustment_id, _dim_session_selected_adjustment_id <= _selection_latest_adjustment_id ),
				    SELECTEDMEASURE()
				)
				
				RETURN _result

	column sessions
		dataType: string
		lineageTag: 0c2b12f9-c495-4803-8730-2c0efe05f38e
		summarizeBy: none
		sourceColumn: Name
		sortByColumn: Ordinal

		annotation SummarizationSetBy = Automatic

	column Ordinal
		dataType: int64
		formatString: 0
		lineageTag: 7c414945-2cd9-443e-ba16-b669aa9e973b
		summarizeBy: sum
		sourceColumn: Ordinal

		annotation SummarizationSetBy = Automatic

