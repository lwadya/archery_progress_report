/// Empty table used for storing all report measures
table 'Measure Table'
	excludeFromModelRefresh
	lineageTag: e06aa0ae-6b63-4802-b6e2-30fd7ea315c9

	measure 'Shot Score Average' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-10-09
			// Description:     Average score of all selected shots
			
			AVERAGE( fact_shot_score[score_end_average] )
		formatString: 0.0
		displayFolder: Shot
		lineageTag: edf13954-720e-48a5-ba68-bd7384bdea2e

	measure 'Shot Score Rolling Average Sessions' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-17
			// Description:     Rolling average score across sessions for all selected shots
			
			CALCULATE(
			    [Shot Score Average],
			    calc_grp_formulas[formula] = "Rolling Average Sessions Backward"
			)
		displayFolder: Shot
		lineageTag: c3457a91-fee8-44fd-826a-36232c6d55ee

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format], "0.0" )

	measure 'Shot Count' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-08-29
			// Description:     Count of shots
			
			COUNTROWS( fact_shot_score )
		formatString: #,0
		displayFolder: Shot
		lineageTag: 21936e43-5cf6-4ee0-8164-46334dea594a

	measure 'Round Score Average' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-12-30
			// Description:     Average round score across all rounds of n shots
			
			VAR _round_shot_count = SELECTEDVALUE( select_round_scoring[shot_count] )
			
			VAR _result =
			AVERAGEX(
			    FILTER(
			        fact_shot_score,
			        fact_shot_score[session_shot_id] >= _round_shot_count
			    ),
			    CALCULATE(
			        SUM( fact_shot_score[score_end_average] ),
			        ALL( fact_shot_score ),
			        WINDOW(
			            1 - _round_shot_count, REL,
			            0, REL,
			            ALLEXCEPT( fact_shot_score, fact_shot_score[session_id] ),
			            ORDERBY( fact_shot_score[shot_id] )
			        )
			    )
			)
			
			RETURN _result
		displayFolder: Round
		lineageTag: 0fc71b9c-19cc-4aaa-bfe2-d72aca981feb

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format] )

	measure 'Shot Count Selected Sessions' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-10-09
			// Description:     Total count of shots for selected sessions
			
			SUM( dim_session[session_shot_count] )
		formatString: 0
		displayFolder: Shot
		lineageTag: 8c24f062-e816-497b-b392-b30fcccc303d

	measure 'Shot Percentage' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-09-08
			// Description:     Selected shots as a percentage of selected sessions
			
			DIVIDE(
			    [Shot Count],
			    [Shot Count Selected Sessions]
			)
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Shot
		lineageTag: 9fb8690f-c78c-4ce0-9944-ce339cc26558

	measure 'End Count' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-10
			// Description:     Count of ends
			
			DISTINCTCOUNT( fact_shot_score[end_id] )
		formatString: #,0
		displayFolder: End
		lineageTag: 7ed5ffc0-eb90-4eb8-9a92-56c24da1df70

	measure 'Session ID Min' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-11-08
			// Description:     Min session_id value for selected sessions
			
			MIN( dim_session[session_id] )
		formatString: 0
		displayFolder: Session
		lineageTag: 31d29bbe-3c75-47f2-ba72-ed94353d7b20

	measure 'Session ID Max' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-11-08
			// Description:     Max session_id value for selected sessions
			
			MAX( dim_session[session_id] )
		formatString: 0
		displayFolder: Session
		lineageTag: 697ae780-a074-40ae-a780-d34514a361ad

	measure 'Session Count' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-12-23
			// Description:     Count of selected dim_session rows
			
			COUNTROWS( dim_session )
		formatString: #,0
		displayFolder: Session
		lineageTag: fc368630-612a-4a05-b6ac-2b39facfdf35

	measure 'Round Score Max' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-12-30
			// Description:     Max round score across all rounds of n shots
			
			VAR _round_shot_count = SELECTEDVALUE( select_round_scoring[shot_count] )
			
			VAR _result =
			MAXX(
			    FILTER(
			        fact_shot_score,
			        fact_shot_score[session_shot_id] >= _round_shot_count
			    ),
			    CALCULATE(
			        SUM( fact_shot_score[score_end_average] ),
			        ALL( fact_shot_score ),
			        WINDOW(
			            1 - _round_shot_count, REL,
			            0, REL,
			            ALLEXCEPT( fact_shot_score, fact_shot_score[session_id] ),
			            ORDERBY( fact_shot_score[shot_id] )
			        )
			    )
			)
			
			RETURN _result
		displayFolder: Round
		lineageTag: ae9066ce-e793-414d-a28b-58d944daafb2

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format] )

	measure 'Round Score Min' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-12-30
			// Description:     Min round score across all rounds of n shots
			
			VAR _round_shot_count = SELECTEDVALUE( select_round_scoring[shot_count] )
			
			VAR _result =
			MINX(
			    FILTER(
			        fact_shot_score,
			        fact_shot_score[session_shot_id] >= _round_shot_count
			    ),
			    CALCULATE(
			        SUM( fact_shot_score[score_end_average] ),
			        ALL( fact_shot_score ),
			        WINDOW(
			            1 - _round_shot_count, REL,
			            0, REL,
			            ALLEXCEPT( fact_shot_score, fact_shot_score[session_id] ),
			            ORDERBY( fact_shot_score[shot_id] )
			        )
			    )
			)
			
			RETURN _result
		displayFolder: Round
		lineageTag: 36547374-7101-4b34-b3ca-e9444d9b0cd8

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format] )

	measure 'Shot Score Rolling Average Shots' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-20
			// Description:     Rolling average shot score by session shot id, window set by select_shot_rolling_average parameter
			
			VAR _window = SELECTEDVALUE( select_shot_rolling_average[shots], 3 )
			
			VAR _result =
			CALCULATE(
			    [Shot Score Average],
			    WINDOW(
			        1 - _window, REL,
			        0, REL,
			        ALLSELECTED( fact_shot_score[session_shot_id] ),
			        ORDERBY( fact_shot_score[session_shot_id] )
			    )
			)
			
			RETURN _result
		formatString: 0.0
		displayFolder: Shot
		lineageTag: 6067fd42-8eef-4625-accc-002321a30470

	measure 'Score Meets Average Shot ID Min' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-16
			// Description:     The first session shot id for selected sessions where the average shot score meets or exceeds the average score across all shots
			
			VAR _hidden_position = -1000
			
			VAR _avg_score_all_shots =
			CALCULATE(
			    [Shot Score Average],
			    ALLSELECTED( fact_shot_score[session_shot_id] )
			)
			
			VAR _shot_id_table =
			ADDCOLUMNS(
			    VALUES( fact_shot_score[session_shot_id] ),
			    "score_meets_average", [Shot Score Rolling Average Shots] >= _avg_score_all_shots
			)
			
			VAR _min_shot_id =
			MINX(
			    _shot_id_table,
			    [session_shot_id]
			)
			
			VAR _min_shot_at_avg =
			MINX(
			    FILTER(
			        _shot_id_table,
			        [score_meets_average]
			    ),
			    [session_shot_id]
			)
			
			VAR _result =
			IF(
			    _min_shot_at_avg > _min_shot_id,
			    _min_shot_at_avg,
			    _hidden_position
			)
			
			RETURN _result
		formatString: 0
		displayFolder: Shot
		lineageTag: 01ef077e-48dd-49f4-82f1-7a76a1a12f3a

	measure 'Score Meets Average Shot ID Max' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-16
			// Description:     The last session shot id for selected sessions where the average shot score meets or exceeds the average score across all shots
			
			VAR _hidden_position = 1000
			
			VAR _avg_score_all_shots =
			CALCULATE(
			    [Shot Score Average],
			    ALLSELECTED( fact_shot_score[session_shot_id] )
			)
			
			VAR _shot_id_table =
			ADDCOLUMNS(
			    VALUES( fact_shot_score[session_shot_id] ),
			    "score_meets_average", [Shot Score Rolling Average Shots] >= _avg_score_all_shots
			)
			
			VAR _max_shot_id =
			MAXX(
			    _shot_id_table,
			    [session_shot_id]
			)
			
			VAR _max_shot_at_avg =
			MAXX(
			    FILTER(
			        _shot_id_table,
			        [score_meets_average]
			    ),
			    [session_shot_id]
			)
			
			VAR _result =
			IF(
			    _max_shot_at_avg < _max_shot_id,
			    _max_shot_at_avg,
			    _hidden_position
			)
			
			RETURN _result
		formatString: 0
		displayFolder: Shot
		lineageTag: ae836bad-5516-41c3-a451-e253840f2de2

	measure 'Session Rank' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-12-30
			// Description:     Session rank by highest session average shot score
			
			RANK(
			    SKIP,
			    ALL( dim_session ),
			    ORDERBY( [Round Score Average], DESC BLANKS LAST, dim_session[session_id], DESC BLANKS LAST )
			)
		formatString: 0
		displayFolder: Session
		lineageTag: 7819e57a-ac6d-42f6-936b-41474bf992ce

	measure 'Session Dates Scores Text' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-02
			// Description:     Display text for selected session dates for the scores pages
			
			VAR _count_selected_sessions = COUNTROWS( dim_session )
			VAR _min_selected_date = MIN( dim_session[date] )
			VAR _max_selected_date = MAX( dim_session[date] )
			
			VAR _max_dataset_date =
			CALCULATE(
			    MAX( dim_session[date] ),
			    ALL( dim_session )
			)
			
			VAR _result =
			SWITCH(
			    TRUE,
			    _max_selected_date = _max_dataset_date, _max_selected_date & " (latest session, select other dates below)",
			    _count_selected_sessions > 1, _min_selected_date & " - " & _max_selected_date & " (" & _count_selected_sessions & " sessions)",
			    _max_selected_date
			)
			
			RETURN _result
		displayFolder: Display Text
		lineageTag: 2419cabb-9bf8-4f68-92c4-7aa25ea7a1d6

		annotation PBI_FormatHint = {"isText":true}

	measure 'Session Adjustment Most Recent' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-23
			// Description:     Most recent major adjustment for the selected session
			
			SELECTEDVALUE( dim_session[latest_major_adjustment] )
		displayFolder: Adjustments
		lineageTag: d32f3cd1-7abc-41f6-8a30-24f907aa5281

		annotation PBI_FormatHint = {"isText":true}

	measure 'Round Selected Type' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-12-31
			// Description:     Selected round type as defined by the select_round_type table
			
			SELECTEDVALUE( select_round_scoring[round_scoring] )
		displayFolder: Round
		lineageTag: b14d1817-ed85-4c4d-b45a-9253e0db7d43

		annotation PBI_FormatHint = {"isText":true}

	measure 'Days Since Previous Session' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-13
			// Description:     Average number of days since the previous session
			
			AVERAGE( dim_session[days_since_previous_session] )
		formatString: 0
		displayFolder: Time
		lineageTag: 6733a7a0-92d5-4f2d-b979-a6c92845316a

	measure 'Shot Score Performance Against Benchmark' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-15
			// Description:     Shot Score Average minus benchmark value multiplied by round shot count
			
			CALCULATE(
			    [Shot Score Average],
			    calc_grp_correlations[formula] = "Neighboring Sessions Difference"
			)
		displayFolder: Shot
		lineageTag: 9721f256-1e14-415d-b8eb-2a335bb676ff

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format], "0.0" )

	measure 'Shot Count Per End Avg' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-13
			// Description:     Average count of shots per end
			
			AVERAGE( dim_session[shot_count_per_end] )
		formatString: 0.0
		displayFolder: End
		lineageTag: 1fe63990-cd04-474d-99b5-45ff3b969281

	measure 'Day of Week Number' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-13
			// Description:     Average day of week number for the selected date or session
			
			CALCULATE(
			    AVERAGE( dim_calendar[day_of_week_number] ),
			    CROSSFILTER( dim_session[date], dim_calendar[date], Both )
			)
		formatString: 0
		displayFolder: Time
		lineageTag: 6f6ffb2e-f9cc-4d0a-bc38-abf1057017c4

	measure 'Shot Count Per Session Max' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-03
			// Description:     Max count of shots per session
			
			MAX( fact_shot_score[session_shot_id] )
		formatString: 0
		displayFolder: Session
		lineageTag: 16a5fb45-9073-4aa6-aee1-861dbfcf8e12

	measure 'Session Date Range Text' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-20
			// Description:     Display text for selected date range for the Fatigue Effects page
			
			VAR _range_end_date =
			CALCULATE(
			    [Date Max],
			    calc_grp_sessions[sessions] = "Selected Range"
			)
			
			VAR _range_start_date =
			CALCULATE(
			    [Date Min],
			    calc_grp_sessions[sessions] = "Selected Range"
			)
			
			VAR _range_length =
			CALCULATE(
			    [Session Count],
			    calc_grp_sessions[sessions] = "Selected Range"
			)
			
			VAR _result =
			_range_start_date & " - " & _range_end_date & " (" & _range_length & " sessions)"
			
			RETURN _result
		displayFolder: Display Text
		lineageTag: 708de027-4d4c-43fc-bab4-07d0bb071a5b

		annotation PBI_FormatHint = {"isText":true}

	measure 'Data Table Values' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-11
			// Description:     Values for raw data matrix
			
			// Get selected values from sort settings table
			VAR _selected_sort_column = SELECTEDVALUE( select_data_table_sort[column_name] )
			VAR _selected_sort_multiplier = SELECTEDVALUE( select_data_table_sort[sort_multiplier] )
			
			// Get selected values from matrix columns table
			VAR _selected_column = SELECTEDVALUE( select_data_table_columns[column_name], _selected_sort_column )
			VAR _is_total = NOT( HASONEVALUE( select_data_table_columns[column_name] ) )
			
			// Calculate value of selected cell
			VAR _cell_value =
			SWITCH(
			    _selected_column,
			    "Date", MAX( dim_session[date] ),
			    "Session Avg Score", [Shot Score Average],
			    "Days Since Prev Session", [Days Since Previous Session],
			    "Total Ends", [End Count],
			    "Total Shots", [Shot Count],
			    "Shots per End", [Shot Count Per End Avg],
			    "Notes", [Data Table Notes Icon Text],
			    [Data Table End Score Average]
			)
			
			// Apply multiplier if value is a total and used for sorting
			VAR _result =
			IF(
			    _is_total,
			    _cell_value * _selected_sort_multiplier,
			    _cell_value
			)
			
			RETURN _result
		displayFolder: Data Table
		lineageTag: 23463c1b-4620-44e4-ae0a-aef35606aac8

		formatStringDefinition =
				IF(
				    SELECTEDVALUE( select_data_table_columns[level] ) = "end" && [Data Table Values] = 10,
				    "0",
				    SELECTEDVALUE( select_data_table_columns[display_format] )
				)

	measure 'Data Table Colors' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-12
			// Description:     Conditional background color index for raw data matrix
			
			VAR _selected_level = SELECTEDVALUE( select_data_table_columns[level] )
			
			VAR _data_table_color =
			CALCULATE(
			    [Data Table Values],
			    calc_grp_formulas[formula] = "Data Table Score Color"
			)
			
			VAR _result =
			IF(
			    _selected_level = "end",
			    _data_table_color
			)
			
			RETURN _result
		displayFolder: Data Table
		lineageTag: 7a63bb75-4447-46f2-a85d-90d2f2bbc48e

		annotation PBI_FormatHint = {"isText":true}

	measure 'Session Adjustment Selected' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-08
			// Description:     Major adjustment for the selected session
			
			SELECTEDVALUE( dim_session[major_adjustment] )
		displayFolder: Adjustments
		lineageTag: 7fee0b68-2437-4541-84ce-8a32069dcd80

		annotation PBI_FormatHint = {"isText":true}

	measure 'Data Table Notes Icon Text' = ```
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-12
			// Description:     Text icons for raw data matrix Notes column
			
			VAR _has_notes = NOT( ISBLANK( SELECTEDVALUE( dim_session[notes] ) ) )
			VAR _has_major_adjustment = NOT( ISBLANK( SELECTEDVALUE( dim_session[major_adjustment] ) ) )
			
			VAR _result = 
			IF( _has_major_adjustment, UNICHAR( 9873 ) & " " ) &
			IF( _has_notes, UNICHAR( 9998 ) & " " )
			
			RETURN _result
			```
		displayFolder: Data Table
		lineageTag: 5002268e-7c52-4c2c-ad18-27170a517a45

		annotation PBI_FormatHint = {"isText":true}

	measure 'Data Table End Score Average' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-12
			// Description:     Average score for the selected end, used in raw data matrix
			
			VAR _selected_column = SELECTEDVALUE( select_data_table_columns[column_name] )
			VAR _selected_level = SELECTEDVALUE( select_data_table_columns[level] )
			
			VAR _result =
			IF(
			    _selected_level = "end",
			    CALCULATE(
			        [Shot Score Average],
			        fact_shot_score[end] = CONVERT( _selected_column, INTEGER )
			    )
			)
			
			RETURN _result
		formatString: 0.0
		displayFolder: Data Table
		lineageTag: 48389aef-5cf3-4477-9ce5-fe0470a6ecde

	measure 'Data Tooltip Shot Score Average' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-13
			// Description:     Average score for an individual shot based on the end in context in the raw data matrix
			
			VAR _selected_end = SELECTEDVALUE( select_data_table_columns[column_id] ) -- Use column_id because it's already of type int
			
			VAR _result =
			CALCULATE(
			    AVERAGE( fact_shot_score[score] ),
			    fact_shot_score[end] = _selected_end
			)
			
			RETURN _result
		displayFolder: Data Table
		lineageTag: b7df8f34-e9d1-4902-9ef3-e49c711b811f

		formatStringDefinition =
				IF(
				    HASONEVALUE( fact_shot_score[session_shot_id] ),
				    "0",
				    "0.0"
				)

	measure 'Data Tooltip Shot Score Color' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-12
			// Description:     Background color for an individual shot based on the end in context in the raw data matrix
			
			CALCULATE(
			    [Data Tooltip Shot Score Average],
			    calc_grp_formulas[formula] = "Data Table Score Color"
			)
		displayFolder: Data Table
		lineageTag: 85a71d61-04c3-4cfc-a65b-51c1896ede0c

		annotation PBI_FormatHint = {"isText":true}

	measure 'Session Notes Selected' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-12
			// Description:     Notes for the selected session
			
			SELECTEDVALUE( dim_session[notes] )
		displayFolder: Session
		lineageTag: 80d3fdf6-a272-4106-8296-983466e08961

		annotation PBI_FormatHint = {"isText":true}

	measure 'Data Tooltip Shot Count' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-12
			// Description:     Count of shot records that appear in the data table tooltip
			
			VAR _selected_end = SELECTEDVALUE( select_data_table_columns[column_id] ) -- Use column_id because it's already of type int
			
			VAR _result =
			CALCULATE(
			    [Shot Count],
			    fact_shot_score[end] = _selected_end
			)
			
			RETURN _result
		formatString: 0
		displayFolder: Data Table
		lineageTag: 65c12060-32ca-408e-9dec-c3585085200f

	measure 'Data Tooltip Color Text' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-13
			// Description:     Text color with transparency switch for raw data matrix tooltip
			
			IF(
			    SELECTEDVALUE( select_data_table_columns[column_name] ) = "Notes",
			    "#000000FF",
			    "#00000000"
			)
		displayFolder: Data Table
		lineageTag: 04b56f8b-0bc9-4dec-b596-c4990268e2f4

		annotation PBI_FormatHint = {"isText":true}

	measure 'Data Tooltip Color Background' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-13
			// Description:     Background color with transparency switch for raw data matrix tooltip
			
			IF(
			    SELECTEDVALUE( select_data_table_columns[column_name] ) = "Notes",
			    "#FFFFFFFF",
			    "#FFFFFF00"
			)
		displayFolder: Data Table
		lineageTag: 3adb561b-6f68-4b73-9ce7-249936ebac4e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Shot Score Adjustment Against Benchmark' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-17
			// Description:     Shot Score Average at major adjustment minus benchmark value multiplied by round shot count
			
			CALCULATE(
			    [Shot Score Average],
			    calc_grp_formulas[formula] = "Adjustment Difference"
			)
		displayFolder: _Delete
		lineageTag: 89dc8f4b-6c5f-499f-a4b4-d9bd53c8fc38

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format], "0.0" )

	measure 'Round Score Pace' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-10
			// Description:     Round pace for any given shot, equal to average shot score multiplied by round shot count
			
			[Shot Score Average] * SELECTEDVALUE( select_round_scoring[shot_count], 1 )
		displayFolder: Round
		lineageTag: eb1a1150-40d3-4d7a-8661-a05a1248b70c

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format] )

	measure 'Round Score Pace Rolling Average Shots' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-10
			// Description:     Rolling average round pace, equal to rolling average shot score multiplied by round shot count
			
			[Shot Score Rolling Average Shots] * SELECTEDVALUE( select_round_scoring[shot_count], 1 )
		displayFolder: Round
		lineageTag: 773dc509-483a-485c-957e-6f524d58d7ff

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format] )

	measure 'Round Score Pace Rolling Average Sessions' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-19
			// Description:     Rolling average round pace over n sessions, equal to rolling average shot score multiplied by round shot count
			
			[Shot Score Rolling Average Sessions] * SELECTEDVALUE( select_round_scoring[shot_count], 1 )
		displayFolder: Round
		lineageTag: 2577dbb2-6d49-4bb9-88c5-2914ab499101

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format] )

	measure 'Session Adjustment Chart Data Label' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-20
			// Description:     Displays most recent major adjustment for a session at the middle of the chart category range
			
			VAR _selected_id = SELECTEDVALUE( dim_session[session_id] )
			VAR _latest_adjustment = SELECTEDVALUE( dim_session[latest_major_adjustment] )
			
			VAR _middle_range_id =
			CALCULATE(
			    INT( (MAX( dim_session[session_id] ) - MIN( dim_session[session_id] )) / 2 + MIN( dim_session[session_id] ) ),
			    FILTER(
			        ALL( dim_session ),
			        dim_session[latest_major_adjustment] = _latest_adjustment
			    )
			)
			
			VAR _result =
			IF(
			    _selected_id = _middle_range_id,
			    _latest_adjustment
			)
			
			RETURN _result
		displayFolder: _Delete
		lineageTag: 23789799-a4bf-4969-ba25-1a8a6d539649

		annotation PBI_FormatHint = {"isText":true}

	measure 'Round Score Min Description' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-23
			// Description:     Descriptive text for round score min metric
			
			SELECTEDVALUE( select_round_scoring[min_description] )
		displayFolder: Display Text
		lineageTag: 09904902-e4ca-4646-94b0-04cc0a5e912d

		annotation PBI_FormatHint = {"isText":true}

	measure 'Round Score Max Description' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-23
			// Description:     Descriptive text for round score max metric
			
			SELECTEDVALUE( select_round_scoring[max_description] )
		displayFolder: Display Text
		lineageTag: 30910a5f-7ecd-48d6-b5d5-e763860cf498

		annotation PBI_FormatHint = {"isText":true}

	measure 'Round Score Average Description' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-23
			// Description:     Descriptive text for round score average metric
			
			SELECTEDVALUE( select_round_scoring[average_description] )
		displayFolder: Display Text
		lineageTag: 105ee184-f153-4cc4-9029-0e139a520a89

		annotation PBI_FormatHint = {"isText":true}

	measure 'Round Selected Type Description' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-01-23
			// Description:     Descriptive text for the selected round type
			
			SELECTEDVALUE( select_round_scoring[round_description] )
		displayFolder: Display Text
		lineageTag: 079cc39a-8d9c-43ca-a46b-6681c165dc0e

		annotation PBI_FormatHint = {"isText":true}

	measure 'Round Score Time Series Chart Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-04
			// Description:     Dynamic title for round score time series chart
			
			VAR _round_shot_count = SELECTEDVALUE( select_round_scoring[shot_count] )
			
			VAR _result =
			IF(
			    _round_shot_count = 1,
			    "Average end scores for each shot in the selected sessions",
			    "Round scores for each range of " & _round_shot_count & " shots in the selected sessions"
			)
			
			RETURN _result
		displayFolder: Display Text
		lineageTag: fc1b4192-119d-4250-8148-2b1992cfdeac

		annotation PBI_FormatHint = {"isText":true}

	measure 'Session Range Warning Text' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-20
			// Description:     Display text for session range slicer warning if full session window cannot be selected
			
			VAR _session_count = COUNTROWS( dim_session )
			VAR _session_window = SELECTEDVALUE( select_session_range[sessions] )
			
			VAR _result =
			IF(
			    _session_count < _session_window,
			    UNICHAR( 9888 ) & " Date range has fewer than " & _session_window & " sessions " & UNICHAR( 9888 ),
			    ""
			)
			
			RETURN _result
		displayFolder: Display Text
		lineageTag: 4ba731b7-116c-475d-b2fd-2f83c41ae10f

		annotation PBI_FormatHint = {"isText":true}

	measure 'Round Score Pace Line Chart Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-10
			// Description:     Dynamic title for round score pace line chart
			
			VAR _round_shot_count = SELECTEDVALUE( select_round_scoring[shot_count] )
			
			VAR _result =
			IF(
			    _round_shot_count = 1,
			    "Average score by shot",
			    "Average scoring pace for a " & _round_shot_count & "-shot round by shot"
			)
			
			RETURN _result
		displayFolder: Display Text
		lineageTag: 4e3b1945-dc22-4043-aa1a-5bfb7c76574e

		annotation PBI_FormatHint = {"isText":true}

	measure 'Session Count Minimum Filter' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-14
			// Description:     Returns 1 if session end date passes filter, 0 if not
			//                  Filter to optionally exclude values calculated from a session range smaller than
			//                  the specified window from time series line chart in Fatigue Effects page
			
			VAR _range_is_complete =
			IF(
			    MAX( select_session_end_date_time_series[session_id] ) >= SELECTEDVALUE( select_session_range[sessions] ),
			    1,
			    0
			)
			
			VAR _result =
			IF(
			    COUNTROWS( select_boolean ) == 1,
			    _range_is_complete,
			    1
			)
			
			RETURN _result
		formatString: 0
		displayFolder: Time
		lineageTag: a6abb858-79ea-4c18-b5d9-2dfb75a77ca7

	measure 'Fields Fatigue Selected Meas' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-13
			// Description:     Returns the value of the selected measure in the field parameter fields_fatigue_scatter_axis
			//                  Changes to existing field parameter items update automatically, but new items need to be added to SWITCH statement
			
			VAR _measure_name =
			SELECTCOLUMNS(
			    SUMMARIZE(
			        fields_fatigue_measure,
			        fields_fatigue_measure[fields_fatigue_measure],
			        fields_fatigue_measure[fields_fatigue_measure Fields]
			    ),
			    "parameter", [fields_fatigue_measure Fields]
			)
			
			VAR _result =
			SWITCH(
			    _measure_name,
			    NAMEOF( [Days Since Previous Session] ), [Days Since Previous Session],
			    NAMEOF( [Shot Count Session Avg] ), [Shot Count Session Avg],
			    NAMEOF( [Shot Count Per End Avg] ), [Shot Count Per End Avg],
			    NAMEOF( [Day of Week Number] ), [Day of Week Number]
			)
			
			RETURN _result
		formatString: 0.0
		displayFolder: Correlations
		lineageTag: 6a2ee866-e5ed-4f83-b76d-b1680c6a5b97

	measure 'Fields Fatigue Scatter X Axis Max' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-12
			// Description:     Maximum bound for fatigue scatter chart x axis
			
			VAR _max_value =
			MAXX(
			    ALL( dim_session[date] ),
			    [Fields Fatigue Selected Meas]
			)
			
			VAR _std_value =
			STDEVX.P(
			    ALL( dim_session[date] ),
			    [Fields Fatigue Selected Meas]
			)
			
			VAR _result = _max_value + (_std_value * .1)
			
			RETURN _result
		displayFolder: Display Text
		lineageTag: cb1dd65c-4e86-4992-a31d-4fa3b6ba95bf

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Fields Fatigue Scatter X Axis Min' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-12
			// Description:     Minimum bound for fatigue scatter chart x axis
			
			VAR _min_value =
			MINX(
			    ALL( dim_session[date] ),
			    [Fields Fatigue Selected Meas]
			)
			
			VAR _std_value =
			STDEVX.P(
			    ALL( dim_session[date] ),
			    [Fields Fatigue Selected Meas]
			)
			
			VAR _result = _min_value - (_std_value * .1)
			
			RETURN _result
		displayFolder: Display Text
		lineageTag: 115cd53b-acc0-411f-861b-7ff0b3ea1b74

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Fields Fatigue Column Order' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-13
			// Description:     Rank order of fatigue column chart categories for sorting
			
			VAR _measure_name =
			SELECTCOLUMNS(
			    SUMMARIZE(
			        fields_fatigue_column,
			        fields_fatigue_column[fields_fatigue_column],
			        fields_fatigue_column[fields_fatigue_column Fields]
			    ),
			    "parameter", [fields_fatigue_column Fields]
			)
			
			// Return the rank value from the sort columns for each of the column options
			// if a sort column doesn't exist then use the column itself
			VAR _result =
			SWITCH(
			    _measure_name,
			    NAMEOF( dim_session[days_since_previous_session_buckets] ), RANK( DENSE, ALL( dim_session[days_since_previous_session_buckets] ), ORDERBY( dim_session[days_since_previous_session_buckets], ASC ), LAST ),
			    NAMEOF( dim_session[session_shot_count_buckets] ), RANK( DENSE, ALL( dim_session[session_shot_count_rounded] ), ORDERBY( dim_session[session_shot_count_rounded], ASC ), LAST ),
			    NAMEOF( dim_session[shot_count_per_end_buckets] ), RANK( DENSE, ALL( dim_session[shot_count_per_end_rounded] ), ORDERBY( dim_session[shot_count_per_end_rounded], ASC ), LAST ),
			    NAMEOF( dim_calendar[day_of_week_display] ), RANK( DENSE, ALL( dim_calendar[day_of_week_display] ), ORDERBY( dim_calendar[day_of_week_display], ASC ), LAST )
			)
			
			RETURN _result
		formatString: 0
		displayFolder: Correlations
		lineageTag: b5a6d427-872d-4eec-aae7-9c9f3cfe749a

	measure 'Shot Count Session Avg' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-13
			// Description:     Average count of shots for selected sessions
			
			AVERAGE( dim_session[session_shot_count] )
		formatString: 0
		displayFolder: Shot
		lineageTag: 98f1487c-4a31-4ab2-aad7-9fe98e810a09

	measure 'Fields Fatigue Correlation Color' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-13
			// Description:     Color step that corresponds to a correlation coefficient value
			
			CALCULATE(
			    [Fields Fatigue Selected Meas],
			    calc_grp_correlations[formula] = "Performance Color Step"
			)
		formatString: 0
		displayFolder: Correlations
		lineageTag: 4b773b00-c5c5-4ad6-b6b8-0ba92d63e0ae

	measure 'Fields Fatigue Correlation Arrow' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-15
			// Description:     Fatigue factor title and arrow character that corresponds to a correlation coefficient value
			
			SELECTEDVALUE( select_fatigue_factor[factor_name] ) & " " &
			CALCULATE(
			    [Fields Fatigue Selected Meas],
			    calc_grp_correlations[formula] = "Performance Arrow"
			) & ""
		displayFolder: Correlations
		lineageTag: 3b554e58-a8a1-45ba-9331-29b21a9c33ad

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Fields Fatigue Correlation Description' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-13
			// Description:     Descriptive text that corresponds to a correlation coefficient value
			
			CALCULATE(
			    [Fields Fatigue Selected Meas],
			    calc_grp_correlations[formula] = "Performance Description"
			)
		displayFolder: Correlations
		lineageTag: 21b1a84e-8fd9-45f2-8c6e-59dece2e49ae

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Fatigue Column Chart Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-13
			// Description:     Title for points above benchmark column chart on Fatigue Effects page
			
			"Average points above benchmark by " & LOWER( SELECTEDVALUE( select_fatigue_factor[factor_name] ) )
		displayFolder: Display Text
		lineageTag: f7aeb150-ff4b-49fe-aea2-de09d6101775

		annotation PBI_FormatHint = {"isText":true}

	measure 'Fatigue Scatter Chart Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-13
			// Description:     Title for points above benchmark scatter chart on Fatigue Effects page
			
			"Points above benchmark by session date, " & LOWER( SELECTEDVALUE( select_fatigue_factor[factor_name] ) )
		displayFolder: Display Text
		lineageTag: 04667063-64af-47c1-b0e1-b3e530fe27d6

		annotation PBI_FormatHint = {"isText":true}

	measure 'Fatigue Correlation Chart Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-14
			// Description:     Title for correlation coefficient chart on Fatigue Effects page
			
			"Correlation by rolling " & SELECTEDVALUE( select_session_range[sessions] ) & "-session range: performance vs "
			& LOWER( SELECTEDVALUE( select_fatigue_factor[factor_name] ) )
		displayFolder: Display Text
		lineageTag: 70105733-cdee-4678-a3fc-6b7e8a6a961e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Fatigue Factor Time Chart Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-14
			// Description:     Title for fatigue factor time series chart on Fatigue Effects page
			
			"Average " & LOWER( SELECTEDVALUE( select_fatigue_factor[factor_name] ) ) & " by rolling " & SELECTEDVALUE( select_session_range[sessions] ) & "-session range"
		displayFolder: Display Text
		lineageTag: f4fa9765-40e9-4c03-ad68-3630c5f9ce15

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Fatigue Session Range Slicer Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-13
			// Description:     Title for correlation chart range slicer on Fatigue Effects page
			
			"Hide dates with ranges less than " & SELECTEDVALUE( select_session_range[sessions] ) & " sessions"
		displayFolder: Display Text
		lineageTag: f66b84b8-af57-40d6-8fc1-da4d1e6e16db

		annotation PBI_FormatHint = {"isText":true}

	measure 'Fields Fatigue End Date Selected Meas' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-14
			// Description:     Returns the selected fatigue field parameter measure only if the date in filter context is the latest in the date slicer
			
			IF(
			    MAX( select_session_end_date[date] ) == MAX( select_session_end_date_time_series[date] ),
			    [Fields Fatigue Selected Meas]
			)
		formatString: 0.0
		displayFolder: Correlations
		lineageTag: 28cb6b30-cd18-45f7-8c85-0935a5965313

	measure 'Fields Fatigue Correlation Coefficient' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-14
			// Description:     Correlation coefficient value for selected fatigue field parameter measure
			
			CALCULATE(
			    [Fields Fatigue Selected Meas],
			    calc_grp_correlations[formula] = "Performance Coefficient"
			)
		formatString: 0.00
		displayFolder: Correlations
		lineageTag: 97b76532-9251-4bff-af7d-55d6bf97f397

	measure 'Fields Fatigue Correlation Coefficient Positive' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-14
			// Description:     Only positive correlation coefficient values for selected fatigue field parameter measure
			
			VAR _raw_value = [Fields Fatigue Correlation Coefficient]
			
			VAR _result =
			IF(
			    ISBLANK( _raw_value ) || _raw_value > 0,
			    _raw_value,
			    0
			)
			
			RETURN _result
		formatString: 0.00
		displayFolder: Correlations
		lineageTag: 6a4e9513-2560-4e60-b682-0c4648e3f233

	measure 'Fields Fatigue Correlation Coefficient Negative' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-14
			// Description:     Only negative correlation coefficient values for selected fatigue field parameter measure
			
			VAR _raw_value = [Fields Fatigue Correlation Coefficient]
			
			VAR _result =
			IF(
			    ISBLANK( _raw_value ) || _raw_value < 0,
			    _raw_value,
			    0
			)
			
			RETURN _result
		formatString: 0.00
		displayFolder: Correlations
		lineageTag: 57c19332-b6bd-4e8d-9848-a9da8ee8252b

	measure 'Fields Fatigue End Date Correlation Coefficient' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-14
			// Description:     Returns the performance correlation coefficient for the selected fatigue field parameter
			//                  measure only if the date in filter context is the latest in the date slicer
			
			IF(
			    MAX( select_session_end_date[date] ) == MAX( select_session_end_date_time_series[date] ),
			    [Fields Fatigue Correlation Coefficient]
			)
		formatString: 0.00
		displayFolder: Correlations
		lineageTag: a6733b52-8b8d-43ca-883a-0119060d62db

	measure 'Decline Period Shot Count' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-16
			// Description:     Length of decline period in shots, zero if there is no decline
			//                  decline - point in sessions when scores dip below average and never recover
			
			VAR _shot_difference = MAX( dim_session[session_shot_count] ) - [Score Meets Average Shot ID Max]
			
			VAR _result =
			IF(
			    _shot_difference > 0,
			    _shot_difference,
			    0
			)
			
			RETURN _result
		formatString: 0
		displayFolder: Shot
		lineageTag: 2e455886-a9a6-45eb-a595-21c80a1d1adc

	measure 'Warm Up Period Shot Count' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-16
			// Description:     Length of warm up period in shots, zero if there is no warm up period
			//                  warm up - point in sessions when scores first rise above average
			
			VAR _shot_position = [Score Meets Average Shot ID Min]
			
			VAR _result =
			IF(
			    _shot_position > 0,
			    _shot_position,
			    0
			)
			
			RETURN _result
		formatString: 0
		displayFolder: Shot
		lineageTag: 598f2498-cdaf-40c9-b459-bc1f7a6a413a

	measure 'Decline Period Shot Count End Date' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-16
			// Description:     Returns length of decline period only if the date in filter context is the latest in the date slicer
			
			IF(
			    MAX( select_session_end_date[date] ) == MAX( select_session_end_date_time_series[date] ),
			    [Decline Period Shot Count]
			)
		formatString: 0
		displayFolder: Shot
		lineageTag: 3a06e8e4-0e11-42a9-8677-39fdb51df99e

	measure 'Warm Up Period Shot Count End Date' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-16
			// Description:     Returns length of warm up period only if the date in filter context is the latest in the date slicer
			
			IF(
			    MAX( select_session_end_date[date] ) == MAX( select_session_end_date_time_series[date] ),
			    [Warm Up Period Shot Count]
			)
		formatString: 0
		displayFolder: Shot
		lineageTag: a8ebc161-42e6-42ab-bb8a-3ea947bf4b3e

	measure 'Warm Up Time Series Chart Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-16
			// Description:     Title for warm up time series chart on Warm Up and Decline page
			
			"Warm up period length in shots by rolling " & SELECTEDVALUE( select_session_range[sessions] ) & "-session range"
		displayFolder: Display Text
		lineageTag: c1e1af85-14bd-447d-b61b-c1cc3ad80e8b

		annotation PBI_FormatHint = {"isText":true}

	measure 'Decline Time Series Chart Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-16
			// Description:     Title for decline time series chart on Warm Up and Decline page
			
			"Decline period length in shots by rolling " & SELECTEDVALUE( select_session_range[sessions] ) & "-session range"
		displayFolder: Display Text
		lineageTag: 6e181f3a-0093-4c66-a1ae-7100f257e8c1

		annotation PBI_FormatHint = {"isText":true}

	measure 'Shot Score Rolling Average Selected Adjustment' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-18
			// Description:     Rolling average score across sessions for which the selected major adjustments are the latest
			
			CALCULATE(
			    [Shot Score Rolling Average Sessions],
			    calc_grp_sessions[sessions] = "Selected Adjustments Time Series"
			)
		displayFolder: Adjustments
		lineageTag: 36a36c21-2e4a-49a2-9609-a5c873d5e3df

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format], "0.0" )

	measure 'Shot Score Rolling Average Selected Adjustment Ends' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-19
			// Description:     Rolling average score for the first and last sessions that pass the calculation group filter
			
			VAR _in_context_session_id = SELECTEDVALUE( dim_session[session_id] )
			
			VAR _result =
			IF(
			    OR( _in_context_session_id = [Session Adjustment Selected ID Min], _in_context_session_id = [Session Adjustment Selected ID Max] ),
			    [Shot Score Rolling Average Selected Adjustment]
			)
			
			RETURN _result
		displayFolder: Adjustments
		lineageTag: 2c14ddc8-6bbd-4721-9b91-e46449d76656

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format], "0.0" )

	measure 'Shot Score Rolling Average Selected Adjustment Range Difference' = ```
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-19
			// Description:     Difference between rolling average score for the first session and last session for the selected adjustments
			
			VAR _decimal_count = 
			IF(
			    SELECTEDVALUE( select_round_scoring[shot_count], 1 ) = 1,
			    1,
			    0
			)
			
			VAR _start_id = [Session Adjustment Selected ID Min]
			
			VAR _end_id = [Session Adjustment Selected ID Max]
			
			VAR _start_value =
			CALCULATE(
			    [Shot Score Rolling Average Sessions],
			    FILTER(
			        ALL( dim_session ),
			        dim_session[session_id] = _start_id
			    )
			)
			
			VAR _end_value =
			CALCULATE(
			    [Shot Score Rolling Average Sessions],
			    FILTER(
			        ALL( dim_session ),
			        dim_session[session_id] = _end_id
			    )
			)
			
			VAR _result = ROUND( _end_value - _start_value, _decimal_count )
			
			RETURN _result
			```
		displayFolder: Adjustments
		lineageTag: 8db9bf62-062b-4fed-8557-84f65338de98

		formatStringDefinition = SELECTEDVALUE( select_round_scoring[display_format], "0.0" )

	measure 'Session Adjustment Count' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-18
			// Description:     Count of sessions with selected adjustments as their latest adjustment
			
			CALCULATE(
			    [Session Count],
			    calc_grp_sessions[sessions] = "Selected Adjustments"
			)
		formatString: 0
		displayFolder: Adjustments
		lineageTag: 12efd68d-5dd0-444a-9b57-7abd4182740b

	measure 'Session Adjustment Selected ID Min' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-19
			// Description:     First session id for the selected adjustments
			
			CALCULATE(
			    [Session ID Min],
			    calc_grp_sessions[sessions] = "Selected Adjustments"
			)
		formatString: 0
		displayFolder: Adjustments
		lineageTag: a691542c-e673-47db-b4cc-0ac2b4505ddf

	measure 'Session Adjustment Selected ID Max' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-19
			// Description:     Latest session id for the selected adjustments
			
			CALCULATE(
			    [Session ID Max],
			    calc_grp_sessions[sessions] = "Selected Adjustments"
			)
		formatString: 0
		displayFolder: Adjustments
		lineageTag: 8543bf24-29a3-43aa-a064-423a4f162d00

	measure 'Session Adjustment in Date Range' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-19
			// Description:     Returns 1 if any sessions for each adjustment are within the selected date range
			
			IF(
			    CONTAINS(
			        VALUES( dim_session[latest_adjustment_session_id] ),
			        dim_session[latest_adjustment_session_id],
			        SELECTEDVALUE( select_major_adjustment[session_id] )
			    ),
			    1,
			    0
			)
		formatString: 0
		displayFolder: Adjustments
		lineageTag: d727d407-821d-4f30-bf30-159802f423fd

	measure 'Session Adjustment Date Range Text' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-20
			// Description:     Display text for selected date range for the Adjustments page
			
			VAR _range_end_date =
			CALCULATE(
			    [Date Max],
			    calc_grp_sessions[sessions] = "Selected Adjustments"
			)
			
			VAR _range_start_date =
			CALCULATE(
			    [Date Min],
			    calc_grp_sessions[sessions] = "Selected Adjustments"
			)
			
			VAR _range_length =
			CALCULATE(
			    [Session Count],
			    calc_grp_sessions[sessions] = "Selected Adjustments"
			)
			
			VAR _latest_adjustment_start_date =
			CALCULATE(
			    MAX( select_major_adjustment[date] ),
			    ALL( select_major_adjustment )
			)
			
			VAR _result =
			_range_start_date & " - " & _range_end_date & " (" & _range_length & " sessions" &
			IF(
			    _range_start_date = _latest_adjustment_start_date,
			    ", latest adjustment)",
			    ")"
			)
			
			RETURN _result
		displayFolder: Display Text
		lineageTag: 9d2cce10-32a2-40c6-81f3-e5d6b75036ec

		annotation PBI_FormatHint = {"isText":true}

	measure 'Date Min' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-20
			// Description:     Min dim_calendar date value
			
			MIN( dim_calendar[date] )
		displayFolder: Time
		lineageTag: 92435634-876c-4260-bcf2-543580d0c97b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Date Max' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-20
			// Description:     Max dim_calendar date value
			
			MAX( dim_calendar[date] )
		displayFolder: Time
		lineageTag: e2a99b2d-7e7b-4503-b953-a7b64ef0ffce

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Session Adjustment Time Series Chart Title' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-20
			// Description:     Title for correlation coefficient chart on Fatigue Effects page
			
			"Average score by rolling " & SELECTEDVALUE( select_session_range[sessions] ) & "-session range (selected adjustment periods highlighted)"
		displayFolder: Display Text
		lineageTag: bcdb8b56-d86f-47dc-af18-6f1cdc305cc6

		annotation PBI_FormatHint = {"isText":true}

	measure 'Factor BG Color Days Since Previous' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-20
			// Description:     Background shape color for Days Since Previous Session factor card
			
			IF(
			    SELECTEDVALUE( select_fatigue_factor[factor_name] ) = "Days Since Previous Session",
			    "#FFFFFFFF",
			    "#FFFFFF00"
			)
		displayFolder: Correlations
		lineageTag: 1ae1d4f5-51f8-43d8-9fcd-6f8844a0a706

		annotation PBI_FormatHint = {"isText":true}

	measure 'Factor BG Color Shots per Session' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-20
			// Description:     Background shape color for Shots per Session factor card
			
			IF(
			    SELECTEDVALUE( select_fatigue_factor[factor_name] ) = "Shots per Session",
			    "#FFFFFFFF",
			    "#FFFFFF00"
			)
		displayFolder: Correlations
		lineageTag: b4d64ea3-b38c-4879-81ed-90bce1bfa609

		annotation PBI_FormatHint = {"isText":true}

	measure 'Factor BG Color Shots per End' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-20
			// Description:     Background shape color for Shots per End factor card
			
			IF(
			    SELECTEDVALUE( select_fatigue_factor[factor_name] ) = "Shots per End",
			    "#FFFFFFFF",
			    "#FFFFFF00"
			)
		displayFolder: Correlations
		lineageTag: 777f48a9-01b2-41c2-9751-04a3f8586ed9

		annotation PBI_FormatHint = {"isText":true}

	measure 'Factor BG Color Day of Week' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2024-02-20
			// Description:     Background shape color for Day of Week factor card
			
			IF(
			    SELECTEDVALUE( select_fatigue_factor[factor_name] ) = "Day of Week",
			    "#FFFFFFFF",
			    "#FFFFFF00"
			)
		displayFolder: Correlations
		lineageTag: ad4b954a-0b5a-4d17-8704-c24f7dbcf62f

		annotation PBI_FormatHint = {"isText":true}

	measure 'Session Range Warning BG Color' =
			
			// Last Developer:  Lukas Wadya
			// Last Updated:    2025-02-20
			// Description:     Background color for shape warning if full session range window cannot be selected
			
			IF(
			    LEN( [Session Range Warning Text] ) > 0,
			    "#E83636FF",
			    "#E8363600"
			)
		displayFolder: Display Text
		lineageTag: 9fb84795-adcf-4daa-bf3f-0f86a9459be7

		annotation PBI_FormatHint = {"isText":true}

	partition 'Measure Table' = m
		mode: import
		queryGroup: 'Table Queries'
		source = #table( {}, {} )

	annotation PBI_ResultType = Table

	annotation PBI_NavigationStepName = Navigation

